buildscript {
    ext {
    }
}

plugins {
    id 'org.springframework.boot' version "$_springBootVersion"
    id 'io.spring.dependency-management' version "$_springDependencyManagement"
    id 'java'
    id 'nu.studer.jooq' version "$_jooqGradlePluginVersion"
}

group = 'com.sunnymix'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // spring
    implementation "org.springframework.boot:spring-boot-starter-web:$_springBootVersion"
    // lombok
    compileOnly "org.projectlombok:lombok:$_lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$_lombokVersion"
    // mysql
    runtimeOnly "mysql:mysql-connector-java:$_mysqlVersion"
    // jooq
    implementation "org.springframework.boot:spring-boot-starter-jooq:$_springJooqVersion"
    compileOnly "org.jooq:jooq-meta:$_jooqVersion"
    compileOnly "org.jooq:jooq-codegen:$_jooqVersion"
    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Oss
    implementation "com.aliyun.oss:aliyun-sdk-oss:3.10.2"
}

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets {
    orgmain {
        java.srcDirs files('src/main/java', 'gensrc/main/java')
    }
}

jooq {
    version = _jooqVersion

    dependencies {
        jooqGenerator "mysql:mysql-connector-java:$_mysqlVersion"
    }

    configurations {
        main {
            def schema = 'wingdoc'
            def basePackageName = 'com.sunnymix.wingdoc.dao.jooq'
            def targetDirectory = "$projectDir/gensrc/main/java"

            generateSchemaSourceOnCompilation = false

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.INFO

                jdbc {
                    driver = 'com.mysql.jdbc.Driver'
                    url = "$_dbHost/$schema?useUnicode=true&characterEncoding=UTF-8&useSSL=false"
                    user = _dbUser
                    password = _dbPassword
                }

                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = schema
                        outputSchema = schema
                        includes = '.*'
                        excludes = 'flyway_schema_history'

                        forcedTypes {
                            forcedType {
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = basePackageName
                        directory = targetDirectory
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}
